//Doom Player Class
ACTOR DoomMarine : PlayerPawn 
{
	Health 270
	Player.Maxhealth 270
	Mass 150
	Height 56
	Radius 16
	Speed 1
	PainChance 255
    Player.Jumpz 12
    Damagefactor "Annihilate", .7
	Damagefactor "HeroSuppression", .5
	Damagefactor "Heal", 0
	+NORADIUSDMG
	Player.ColorRange 112, 127
	Player.DisplayName "DoomMarine"
	Player.CrouchSprite "PLYC"
	Player.StartItem "SpawnArmor", 1
	Player.StartItem "DoomClass", 1
	Player.StartItem "ZombiemanSummoner", 1
	Player.StartItem "SergeantSummoner", 1
	Player.Runhealth 120
	Player.Forwardmove 0.85, 0.9 
	Damagefactor "Kaboom", .6
	Damagefactor "VirusBoom", .6
	Damagefactor "Sniper", .5
	DamageFactor "Spectre", .5
	Player.Weaponslot 1, ZombiemanSummoner, SergeantSummoner, ImpSummoner, LostSoulSummoner, PinkyDemonSummoner
	Player.Weaponslot 2, SpectreSummoner, CacodemonSummoner, ChaingunnerSummoner, HellknightSummoner
	Player.Weaponslot 3, revenantsummoner, RevenantRetrofit, RailRevenantSummoner, baronofhellsummoner
	Player.Weaponslot 4, mancubussummoner, MancubusRetrofit, FlameMancubusSummoner, arachnotronsummoner
	Player.Weaponslot 5, painelementalsummoner, Painelementalretrofit, DualPainElementalSummoner, archvilesummoner
	Player.Weaponslot 6, spidermastermindsummoner, cyberdemonsummoner
	Player.Weaponslot 7, VilePulsesummoner
	Player.Weaponslot 8, DoomArmorPurchase
	Player.Weaponslot 9, DoomTeleporterSummoner
	States
	{
	Spawn:
		PLAY A 0
		PLAY A -1
		Goto Spawn
	See:
      	PLAY A 6 A_ChangeFlag("Shadow", false)
		PLAY A 0 A_JumpIfInventory("MonsterRemover", 1, "Missile")
		PLAY A 0 A_CheckFloor(2)
		PLAY A 0 A_Jump(256, 2)
      	PLAY B 0 A_PlaySound("gibbage/xpstep")
		PLAY B 6 A_ChangeFlag("Shadow", false)
		PLAY B 0 A_JumpIfInventory("MonsterRemover", 1, "Missile")
		PLAY C 6 A_ChangeFlag("Shadow", false)
		PLAY C 0 A_JumpIfInventory("MonsterRemover", 1, "Missile")
		PLAY C 0 A_CheckFloor(2)
		PLAY C 0 A_Jump(256, 2)
      	PLAY D 0 A_PlaySound("gibbage/xpstep")
		PLAY D 6 A_ChangeFlag("Shadow", false)
		PLAY A 0 A_JumpIfInventory("MonsterRemover", 1, "Missile")
		goto Spawn
	Missile:
		PLAY F 0 A_AlertMonsters
		PLAY E 12 
		Goto Spawn
	Melee:
		PLAY F 0 A_AlertMonsters
		PLAY F 6 BRIGHT
		Goto Missile
	Pain:
		PLAY G 4 
		PLAY G 4 A_Pain 
		Goto Spawn
    Pain.Sniper:
    Pain.BlueSniper:
	Pain.RedSniper:
	Pain.GreenSniper:
    Pain.SilverSniper:
		PLAY G 0 ACS_NamedExecuteAlways("MonsterMessages",0,100,3)
		PLAY G 4 
		PLAY G 4 A_Pain 
		Goto Spawn
    Pain.Suppression:
    Pain.Suppression2:
	Pain.HeroSuppression:
    Pain.BlueSuppression:
    Pain.BlueSuppression2:
    Pain.BlueSuppression3:
	Pain.BlueHeroSuppression:
    Pain.RedSuppression:
    Pain.RedSuppression2:
    Pain.RedSuppression3:
	Pain.RedHeroSuppression:
    Pain.GreenSuppression:
    Pain.GreenSuppression2:
    Pain.GreenSuppression3:
	Pain.GreenHeroSuppression:
    Pain.SilverSuppression:
    Pain.SilverSuppression2:
    Pain.SilverSuppression3:
	Pain.SilverHeroSuppression:
		PLAY G 0 ACS_NamedExecuteAlways("MonsterMessages",0,100,2)
		PLAY G 4 
		PLAY G 4 A_Pain
		Goto Spawn
   	Death:
     	PLAY H 0 A_Jump (128, 10)
     	PLAY H 5 
     	PLAY I 5 A_PlayerScream
    	PLAY J 5 A_NoBlocking
    	PLAY K 5
        PLAY L 1 A_SpawnItem("Bodythud", 0, 0, 0, 0)
    	PLAY LMN 5 
     	PLAY N -1 A_SpawnItemEX("SpawnArmor2")
    	Stop
   		PLAY P 5 
   		PLAY Q 5 A_PlayerScream
   		PLAY R 5 A_NoBlocking
   		PLAY S 5
   		PLAY T 1 A_SpawnItem("Bodythud", 0, 0, 0, 0)
   		PLAY U 5 
    	PLAY V -1 A_SpawnItemEX("SpawnArmor2")
    	Stop
	XDeath:
      	TNT1 A 0 A_PlaySound("gibbage/xsplat")
       	TNT1 A 0 A_PlaySound("gibbage/playergib") 
      	TNT1 A 0 A_NoBlocking 
       	TNT1 A 0 A_CustomMissile("PLAYERBOOT", 11, 0, 33, 2, 45)
       	TNT1 A 1 A_CustomMissile("PLAYERBOOT", 11, 0, -33, 2, 27)
       	TNT1 A 0 A_CustomMissile("SHOTgunGUYARM", 25, 0, random(0,360), 2, 50)
       	TNT1 A 1 A_CustomMissile("SHOTgunGUYARM", 36, 0, random(0,360), 2, 32)
       	TNT1 A 1 A_SkullPop("Custom_BloodySkull")
       	TNT1 A 1 A_SpawnItem("GIBBER")
       	TNT1 A -1 A_SpawnItemEX("SpawnArmor2")
		stop
	}
}

ACTOR MARINEHEAD
{
    +DOOMBOUNCE
	+NOBLOCKMAP
	+CLIENTSIDEONLY
    -NOGRAVITY
    -NOTELEPORT
    -SOLID
	PROJECTILE
    Radius 3
    Damage 0
    Decal BloodSplat
    Bouncefactor 0.6
    SeeSound "gibbage/xpart"
    Speed 5
    States
    {
    Spawn:
      PLHD A 0 
      PLHD A 0 ThrustThingZ (0, 45, 0, 1)
      goto See 
    See:
      PLHD A 5 A_SpawnItemEx("Blood", 0, 0, 0,0,0,0,0,128)
      PLHD B 5 ThrustThingZ(0,6,1,1)
      PLHD CDABCD 5
      loop
    Death:
      PLHD C 0 A_Jump (16,4)
      PLHD C 0 A_Jump (32,4)
      PLHD C 0 A_Jump (64,4)
      PLHD C 0 A_Jump (128,4)
      PLHD A 50
      Stop
      PLHD B 50
      Stop
      PLHD C 50
      Stop
      PLHD D 50
      Stop
    }
}

ACTOR PLAYERBOOT
{
	PROJECTILE
    +DOOMBOUNCE
	+CLIENTSIDEONLY
	+NOBLOCKMAP
    -NOGRAVITY
    -NOTELEPORT
    -SOLID
    Radius 3
    Damage 0
    Decal BloodSplat
    Bouncefactor 0.4
    SeeSound "gibbage/xpart"
    Speed 5
    States
    {
    Spawn:
      PLG1 A 0 
      PLG1 A 0 ThrustThingZ (0, 45, 0, 1)
      goto See 
    See:
      PLG1 A 5 A_SpawnItemEx("Blood", 0, 0, 0,0,0,0,0,128)
      PLG1 B 5 ThrustThingZ(0,6,1,1)
      PLG1 CABC 5 
      loop
    Death:
      PLG1 C 0 A_Jump (128,3)
      PLG1 C 0 A_Jump (128,3)
      PLG1 C 0 A_Jump (128,3)
      PLG1 A 50 
      Stop
      PLG1 B 50 
      Stop
      PLG1 C 50 
      Stop       
    }
}